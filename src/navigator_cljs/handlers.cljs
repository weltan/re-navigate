(ns navigator-cljs.handlers
  (:require
    [re-frame.core :refer [register-handler after]]
    [cljs.spec :as s]
    [navigator-cljs.db :as db :refer [app-db]]
    [clojure.walk :as cw]))

;; -- Helpers --------------------------------------------------------------

(defn dec-to-zero
  "Same as dec if not zero"
  [arg]
  (if (< 0 arg)
    (dec arg)
    arg))

;; -- Middleware ------------------------------------------------------------
;;
;; See https://github.com/Day8/re-frame/wiki/Using-Handler-Middleware
;;
(defn check-and-throw
  "Throw an exception if db doesn't have a valid spec."
  [spec db]
  (when-not (s/valid? spec db)
    (let [explain-data (s/explain-data spec db)]
      (throw (ex-info (str "Spec check failed: " explain-data) explain-data)))))

(def validate-spec-mw
  (if goog.DEBUG
    (after (partial check-and-throw ::db/app-db))
    []))

;; -- Handlers --------------------------------------------------------------

(register-handler
  :initialize-db
  validate-spec-mw
  (fn [_ _]
    app-db))

(register-handler
  :nav/push
  validate-spec-mw
  (fn [db [_ value]]
    (-> db
        (update-in [:nav :index] inc)
        (update-in [:nav :routes] #(conj % value)))))

(register-handler
  :nav/pop
  validate-spec-mw
  (fn [db [_ _]]
    (-> db
        (update-in [:nav :index] dec-to-zero)
        (update-in [:nav :routes] pop))))

(register-handler
  :nav/home
  validate-spec-mw
  (fn [db [_ _]]
    (-> db
        (assoc-in [:nav :index] 0)
        (assoc-in [:nav :routes] (vector (get-in db [:nav :routes 0]))))))

(register-handler
  :chat/send
  validate-spec-mw
  (fn [db [_ messages]]
    (let [cljs-message (cw/keywordize-keys (js->clj (first messages)))
          _ (.log js/console cljs-message)]
      (-> db
        (update-in [:messages] #(conj % cljs-message))))))